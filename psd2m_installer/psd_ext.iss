; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
; Two {{ is correct here, this is the escape sequence for single {
AppId={{1DE7C2FA-4017-4A54-8318-B012C1F673E1}
AppName=PSD to 3D Photoshop Extension
AppVersion=PLUGIN_VER_TOKEN
AppVerName=PSD to 3D PLUGIN_VER_TOKEN
AppPublisher=EDFilms
AppPublisherURL=http://www.edfilms.net/
AppSupportURL=http://www.edfilms.net/
AppUpdatesURL=http://www.edfilms.net/
DefaultDirName={pf}\PSDto3D
DefaultGroupName=PSDto3D
AllowNoIcons=yes
OutputBaseFilename=setup
Compression=lzma
SolidCompression=yes
ArchitecturesInstallIn64BitMode=x64
UninstallDisplayIcon={app}\blender\icons\PSD to 3D Icons.ico
UninstallDisplayName=PSD to 3D PLUGIN_VER_TOKEN
SetupIconFile=TEMP_PSDto3D_INSTALLER.ico

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl";
Name: "french"; MessagesFile: "compiler:Languages\French.isl";

[Types]
Name: "full";     Description:Full Installation;
Name: "custom";   Description:Custom Installation; Flags: iscustom;

[Components]
Name: extCC; Description: Photoshop Creative Cloud Extension; Types: full custom;
;Flags: fixed

[Tasks]

[Files]
Source: "..\Builds\installer\stage_full\photoshop\cc\Extension\*"; DestDir: "{commoncf32}\Adobe\CEP\extensions\com.EDFilms.PsdExporter"; Flags: ignoreversion recursesubdirs createallsubdirs; Components: extCC;
Source: "..\Builds\installer\stage_full\photoshop\*"; DestDir: "{app}\photoshop"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "..\Builds\installer\stage_full\blender\icons\PSD to 3D Icons.ico"; DestDir: "{app}\blender\icons"; Flags: ignoreversion recursesubdirs createallsubdirs;
Source: "..\Builds\installer\stage_full\docs\Online Documentation.url"; DestDir: "{app}\docs"; Flags: ignoreversion recursesubdirs createallsubdirs;
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\PSD to 3D"; Filename: "{app}\blender\icons\PSD to 3D Icons.ico"; WorkingDir: "{app}\blender\icons"; IconFilename: "{app}\blender\icons\PSD to 3D Icons.ico"; Comment: "PSD to 3D";
Name: "{group}\Online Documentation"; Filename: "{app}\docs\Online Documentation.url"; WorkingDir: "{app}\docs"; IconFilename: "{commonpf32}\Microsoft\Edge\Application\msedge.exe"; Comment: "PSD to 3D Online Documentation";

[CustomMessages]
english.HelpAndSupport=Help & Support
french.HelpAndSupport=Aide et support technique
english.HelpAndSupportEx=
french.HelpAndSupportEx=
; these are in RTF format, edit by saving a RTF file to disk then open as plain text to copy the contents
english.HelpMsg={\rtf1\ansi\ansicpg1252\deff0\nouicompat\deflang13322{\fonttbl{\f0\fnil\fcharset0 Tahoma;}}{\colortbl ;\red0\green0\blue255;}{\*\generator Riched20 10.0.19041}\viewkind4\uc1 \pard\f0\fs16 Thank you for purchasing PSD to FBX!\par\par\par For documentation and tutorials, please visit our website.\par\par{{\field{\*\fldinst{HYPERLINK https://edfilms.notion.site/PSD-to-3D-user-manual-6221458afd534a5aa14a46b77048a396 }}{\fldrslt{Online Documentation}}}}\f0\fs16\par\par\par If you are experiencing difficulties with installation or licensing, please visit our online troubleshooting guide.\par\par{{\field{\*\fldinst{HYPERLINK https://edfilms.notion.site/Troubleshooting-1b487fa562be4d8aa10f0d0fb5d3a1fe }}{\fldrslt{Troubleshooting Guide}}}}\f0\fs22\par}
french.HelpMsg={\rtf1\ansi\ansicpg1252\deff0\nouicompat\deflang13322{\fonttbl{\f0\fnil\fcharset0 Tahoma;}}{\colortbl ;\red0\green0\blue255;}{\*\generator Riched20 10.0.19041}\viewkind4\uc1 \pard\f0\fs16 Merci d\rquote avoir achet\'e9 PSD to FBX!\par\par\par Pour obtenir la documentation et les tutoriels, n\rquote h\'e9sitez pas \'e0 visiter notre site web.\par\par{{\field{\*\fldinst{HYPERLINK https://edfilms.notion.site/PSD-to-3D-user-manual-6221458afd534a5aa14a46b77048a396 }}{\fldrslt{Documentation en ligne}}}}\f0\fs16\par\par\par Si vous \'e9prouvez des difficult\'e9s avec l'assistant d'installation ou avec l'enregistrement de votre licence, veuillez vous r\'e9f\'e9rer \'e0 notre guide de d\'e9pannage en ligne.\par\par{{\field{\*\fldinst{HYPERLINK https://edfilms.notion.site/Troubleshooting-1b487fa562be4d8aa10f0d0fb5d3a1fe }}{\fldrslt{Guide de d\'e9pannage}}}}\f0\fs22\par}

[Code]
var
  OutputMsgWizardPage: TOutputMsgWizardPage;
  AfterID: Integer;
  RichViewer: TRichEditViewer;

procedure InitializeWizard;

begin
  { Create the pages }

  AfterID := wpInfoAfter;
  //AfterID := wpSelectDir; // testing only
  
  OutputMsgWizardPage := CreateOutputMsgPage(AfterID, ExpandConstant('{cm:HelpAndSupport}'), ExpandConstant('{cm:HelpAndSupportEx}'), ExpandConstant('{cm:HelpMsg}'));

  RichViewer := TRichEditViewer.Create(OutputMsgWizardPage);
  RichViewer.Left := 0;
  RichViewer.Top := 0;
  RichViewer.Color := OutputMsgWizardPage.Surface.Color;
  RichViewer.Width := OutputMsgWizardPage.SurfaceWidth;
  RichViewer.Height := OutputMsgWizardPage.SurfaceHeight;
  RichViewer.Parent := OutputMsgWizardPage.Surface;
  RichViewer.BorderStyle := bsNone;
  RichViewer.TabStop := False;
  RichViewer.ReadOnly := True;
  RichViewer.RTFText := ExpandConstant('{cm:HelpMsg}');
  
  AfterID := OutputMsgWizardPage.ID;
end;

